// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {

        general = [
                versionCode: 1,
                versionName: "0.0.1"
        ]

        androidSdk = [
                compileSdkVersion: 26,
                minSdkVersion    : 21,
                targetSdkVersion : 27
        ]

        libVersion = [
                android      : [
                        support: [
                                appCompat : "${androidSdk.targetSdkVersion}.0.2",
                                constraint: '1.0.2'
                        ],
                        gradle : '3.0.1'
                ],
                crashlytics  : '2.7.1',
                fabric       : '1.24.5',
                firebase     : [
                        jobdispatcher: '0.8.4'
                ],
                google       : [
                        dagger: '2.13',
                        gson  : '2.8.2'
                ],
                kotlin       : '1.2.10',
                playPublisher: '1.2.0',
                rx           : [
                        android: '2.0.1',
                        java   : '2.1.7',
                        kotlin : '2.0.0-RC1'
                ],
                squareup     : [
                        okhttp3: '3.9.1'
                ],
                test         : [
                        android : [
                                support    : '1.0.1',
                                uiautomator: '2.1.3'
                        ],
                        assertj : '3.8.0',
                        composer: [
                                plugin: '0.5.0',
                                tester: '0.2.8'
                        ],
                        junit   : '4.12',
                        junit5  : [
                                androidPlugin: '1.0.22',
                                gradle       : '1.0.2',
                                main         : '5.0.2'
                        ],
                        kluent  : '1.30',
                        truth   : '0.37'
                ],
                timber       : '4.6.0',
                threeten     : '1.3.6',
                versionPlugin: '0.17.0'
        ]
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${libVersion.android.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${libVersion.kotlin}"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:${libVersion.test.junit5.androidPlugin}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${libVersion.versionPlugin}"
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}